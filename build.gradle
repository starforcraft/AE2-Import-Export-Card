plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.116'
    id 'idea'
    id 'me.modmuss50.mod-publish-plugin' version '1.0.0'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
        content {
            includeGroup 'org.appliedenergistics'
            includeGroup "de.mari_023"
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    accessTransformers = project.files('src/main/templates/META-INF/accesstransformer.cfg')

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    implementation "org.appliedenergistics:appliedenergistics2:${ae2_version}"

    compileOnly "de.mari_023:ae2wtlib:${ae2wtlib_version}"
    runtimeOnly "de.mari_023:ae2wtlib:${ae2wtlib_version}"

    runtimeOnly "dev.emi:emi-neoforge:${emi_version}"
}

publishMods {
    changelog = current_changelog
    type = STABLE
    modLoaders.add("neoforge")
    file = jar.archiveFile

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "982512"
        minecraftVersions.add(minecraft_version)
        displayName = file.map { it.asFile.name }
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "qelfSMnn"
        minecraftVersions.add(minecraft_version)
    }
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            ae2_version            : ae2_version,
            ae2_version_range      : ae2_version_range,
            ae2wtlib_version       : ae2wtlib_version,
            emi_version            : emi_version,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_url                : mod_url,
            mod_license            : mod_license,
            mod_issue_tracker      : mod_issue_tracker,
            mod_version            : mod_version,
            current_changelog      : current_changelog,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
